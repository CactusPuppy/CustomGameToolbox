// import "settings.lobby";
settings
{
    main
    {
        Mode Name: "CactusPuppy's Toolbox"
        Description: "CactusPuppy's Toolbox | v1.0.0
Code: OVVVC

A set of tools that make custom games more useful for recreating, testing, and recording Overwatch scenarios."
    }
    modes
    {
        General
        {
            Hero Limit: 1 Per Team
            Game Mode Start: Immediately
            Tank Role Passive Health Bonus: Disabled
            Limit Roles: 1 Tank 2 Offense 2 Support
        }

        Hybrid {
            Competitive Rules: On
        }

        Clash {
            Competitive Rules: On
        }
    }
    lobby
    {
        Match Voice Chat: Enabled
    }
}

import "UndoTankHealthReduction.ostw";
import "interface/menu.ostw";
import "lib/logs/logs.del";

String GeneralSettingsCategory: "01. General";
String HeroSettingsCategory: "02. Hero Settings";
String MenuSettingsCategory: "03. Menu Settings";
String ModSettingsCategory: "04. Mod Settings";
String InformationCategory: "05. Information";
String BotAndReplaySettingsCategory: "06. Bot and Replay";
String DebugSettingsCategory: "99. Debug";

globalvar Number i;

// DEBUG: Diagnostics
Number DummyBotCount(): CountOf(FilteredArray(AllPlayers(), IsDummyBot(ArrayElement())));
Number HumanAndAICount(): CountOf(FilteredArray(AllPlayers(), !IsDummyBot(ArrayElement())));
globalvar Color[] gradientColors! = [Color.Green, Color.LimeGreen, Color.Yellow, Color.Orange, Color.Red];
import "OSTWUtils/Colors.del";
void CreateDiagnostic(in String Text, in Color Color, in Number SortOrder) {
  CreateHudText(
    VisibleTo:      HostPlayer(),
    Subheader:      Text,
    Location:       Location.Right,
    SubheaderColor: Color,
    SortOrder:      SortOrder,
    Reevaluation:   HudTextRev.VisibleToStringAndColor,
    Spectators:     Spectators.VisibleAlways
  );
}
rule: "[DEBUG | main.ostw] Show diagnostics"
if (WorkshopSettingToggle(DebugSettingsCategory, "Show Diagnostics", false))
{
  CreateDiagnostic(
    Text:           "[ {0} ]  Server Load".Format([ServerLoad()]),
    Color:          Colors.StepGradient(gradientColors, ServerLoad()/255),
    SortOrder:      1);
  CreateDiagnostic(
    Text:           "[ {0} ]  Server Load Average".Format([ServerLoadAverage()]),
    Color:          Colors.StepGradient(gradientColors, ServerLoadAverage()/255),
    SortOrder:      2);
  CreateDiagnostic(
    Text:           "[ {0} ]  Server Load Peak".Format([ServerLoadPeak()]),
    Color:          Colors.StepGradient(gradientColors, ServerLoadPeak()/255),
    SortOrder:      3);
  CreateDiagnostic(
    Text:           <"[ <0> / <1> ]  Entity Count", EntityCount(), 128>,
    Color:          Colors.StepGradient(gradientColors, EntityCount()/128),
    SortOrder:      4);
  CreateDiagnostic(
    Text:           <"[ <0> / <1> ]  Text Count", TextCount(), 128>,
    Color:          Colors.StepGradient(gradientColors, TextCount() / 128),
    SortOrder:      5);
  CreateDiagnostic(
    Text:           <"[ <0> / <1> ]  Dummy Bot Count", DummyBotCount(), 12 - HumanAndAICount()>,
    Color:          Colors.StepGradient(gradientColors, DummyBotCount() / 12 - HumanAndAICount()),
    SortOrder:      6);
}

globalvar Boolean INSPECTOR_DISABLED! = !WorkshopSettingToggle(DebugSettingsCategory, "Enable Inspector Recording", false);
rule: "[main.ostw] Disable Inspector Recording" -100
if (INSPECTOR_DISABLED)
{
  DisableInspectorRecording();
}

rule: "[main.ostw] Skip assembling heroes"
if (IsAssemblingHeroes())
{
  if (AllPlayers().All(p => p.toolsActionID != ToolsAction.RETURN_TO_ASSEMBLING_HEROES)) {
    SetMatchTime(WorkshopSettingInteger(GeneralSettingsCategory, "Assembling Heroes Length", 5, 0, 30));
  }
  if (isTimePaused) {
    UnpauseMatchTime();
    isTimePaused = false;
    WaitUntil(!IsAssemblingHeroes(), 9999);
    PauseMatchTime();
    isTimePaused = true;
  }
}

rule: "[main.ostw] pause match time on start of round"
if (WorkshopSettingToggle(GeneralSettingsCategory, "Automatically Pause Match Time At Round Start", true))
if (!IsAssemblingHeroes())
{
  PauseMatchTime();
  isTimePaused = true;
}

rule: "[main.ostw] Go back to lobby early, skipping victory poses and POTG"
if (WorkshopSettingToggle(GeneralSettingsCategory, "Skip Post-Match Flow", true))
if (IsMatchComplete())
{
  Wait(3);
  ReturnToLobby();
}

rule: "[main.ostw] Avoid getting people stuck in menu"
Event.OngoingPlayer
if (IsBetweenRounds())
{
  CloseMenu(EventPlayer());
}

rule: "[main.ostw] Skip setup"
if (WorkshopSettingToggle(GeneralSettingsCategory, "Automatically Skip Setup", true))
if (IsInSetup())
{
  SetMatchTime(0);
  if (isTimePaused) {
    UnpauseMatchTime();
    isTimePaused = false;
    WaitUntil(!IsInSetup(), 9999);
    PauseMatchTime();
    isTimePaused = true;
  }
}

// // DEBUG
// rule: "[main.ostw] self-damage on melee"
// Event.OngoingPlayer
// if (WorkshopSettingToggle(GeneralSettingsCategory, "Self-Damage On Melee", false))
// if (IsMeleeing())
// {
//   Damage(EventPlayer(), null, 75);
// }

// import "voicelineFarmingModule.ostw";
