// import "settings.lobby";
import "UndoTankHealthReduction.ostw";
import "interface/menu.ostw";
import "lib/logs/logs.del";
import "lib/quickActions/quickAction.del";

String GeneralSettingsCategory: "01. General";
String HeroSettingsCategory: "02. Hero Settings";
String MenuSettingsCategory: "03. Menu Settings";
String ModSettingsCategory: "04. Mod Settings";
String InformationCategory: "05. Information";
String BotAndReplayCategory: "06. Bot and Replay";
String QuickActionsCategory: "07. Quick Actions";
String DebugCategory: "99. Debug";

globalvar Number i;

// DEBUG: Diagnostics
import "lib/metrics/diagnostics.del";
Number DummyBotCount(): CountOf(FilteredArray(AllPlayers(), IsDummyBot(ArrayElement())));
Number HumanAndAICount(): CountOf(FilteredArray(AllPlayers(), !IsDummyBot(ArrayElement())));

globalvar Boolean INSPECTOR_DISABLED! = !WorkshopSettingToggle(DebugCategory, "Enable Inspector Recording", false);
rule: "[main.ostw] Disable Inspector Recording" -100
if (INSPECTOR_DISABLED)
{
  DisableInspectorRecording();
}

rule: "[main.ostw] If start match immediately, do so"
if (WorkshopSettingToggle(GeneralSettingsCategory, "Start Match Immediately", true))
{
  Wait(2);
  StartGameMode();
}

rule: "[main.ostw] Skip assembling heroes"
if (IsAssemblingHeroes())
{
  if (AllPlayers().All(p => p.toolsActionID != ToolsAction.RETURN_TO_ASSEMBLING_HEROES)) {
    SetMatchTime(WorkshopSettingInteger(GeneralSettingsCategory, "Assembling Heroes Length", 5, 0, 30));
  }
  if (isTimePaused) {
    UnpauseMatchTime();
    isTimePaused = false;
    WaitUntil(!IsAssemblingHeroes(), 9999);
    if (!isTimePaused) PauseMatchTime();
    isTimePaused = true;
  }
}

rule: "[main.ostw] pause match time on start of round"
if (WorkshopSettingToggle(GeneralSettingsCategory, "Automatically Pause Match Time At Round Start", true))
if (IsInSetup() || IsGameInProgress())
{
  AbortIf(isTimePaused);
  if (!isTimePaused) PauseMatchTime();
  isTimePaused = true;
}

rule: "[main.ostw] Go back to lobby early, skipping victory poses and POTG"
if (WorkshopSettingToggle(GeneralSettingsCategory, "Skip Post-Match Flow", true))
if (IsMatchComplete())
{
  Wait(3);
  ReturnToLobby();
}

rule: "[main.ostw] Avoid getting people stuck in menu"
Event.OngoingPlayer
if (IsBetweenRounds())
{
  CloseMenu(EventPlayer());
}

rule: "[main.ostw] Skip setup"
if (WorkshopSettingToggle(GeneralSettingsCategory, "Automatically Skip Setup", true))
if (IsInSetup())
{
  SetMatchTime(0);
  if (isTimePaused) {
    UnpauseMatchTime();
    isTimePaused = false;
    WaitUntil(!IsInSetup(), 9999);
    if (!isTimePaused) PauseMatchTime();
    isTimePaused = true;
  }
}

// // DEBUG
// rule: "[main.ostw] self-damage on melee"
// Event.OngoingPlayer
// if (WorkshopSettingToggle(GeneralSettingsCategory, "Self-Damage On Melee", false))
// if (IsMeleeing())
// {
//   Damage(EventPlayer(), null, 75);
// }

// import "voicelineFarmingModule.ostw";
